//! ARMv7a Linker Script

__page_size = (KERNEL_PAGE_SIZE * 1024);
__virtual_start = KERNEL_VIRTUAL_BASE;
__kernel_start = __virtual_start + KERNEL_BASE;

ENTRY(_start)

SECTIONS
{
  . = __kernel_start;

  .text :
  {
    __text_start = .;

    KEEP(*(.text.boot))
    *(.text)

    . = ALIGN(__page_size);
    __text_end = .;
  }

  .rodata :
  {
    __rodata_start = .;

    *(.rodata)

    . = ALIGN(__page_size);
    __rodata_end = .;
  }

  .data :
  {
    __data_start = .;

    *(.data)

    . = ALIGN(__page_size);
    __data_end = .;
  }

  .bss :
  {
    __bss_start = .;

    bss = .;
    *(.bss .bss.*)

    . = ALIGN(__page_size);
    __bss_end = .;
  }
  
  __bss_size = __bss_end - __bss_start;

  // Reserve space for the kernel stack. Note the reversal of start and end.
  .data.stack :
  {
    __kernel_stack_end = .;
    . += (KERNEL_STACK_PAGES * __page_size);
    __kernel_stack_start = .;
  }

  // Reserve a page for the kernel exception vectors.
  .text.vectors :
  {
    __kernel_exception_vectors_start = .;

    KEEP(*(.text.vectors))

    . = ALIGN(__page_size);
    __kernel_exception_vectors_end = .;
  }

  // Reserve a page for the kernel exception stubs.
  .text.stubs :
  {
    __kernel_exception_stubs_start = .;

    KEEP(*(.text.stubs))

    . = ALIGN(__page_size);
    __kernel_exception_stubs_end = .;
  }

  __kernel_size = __kernel_stack_start - __kernel_start;

  // The L1 table must be aligned on a 16 KiB boundary.
  . = ALIGN(16 * 1024);

  // Reserve 16 KiB for the kernel's bootstrap virtual page table. The L1 table
  // has 4,096 32-bit entries to cover 4 GiB with 1 MiB sections when not using
  // Large Physical Address Extensions. Reserve a single L2 page that will be
  // used to map the exception vectors to the high vectors area.
  .data.pages :
  {
    __kernel_pages_start = .;
    . += (16 * 1024);
    . += __page_size;
    __kernel_pages_end = .;
  }

  __kernel_pages_size = __kernel_pages_end - __kernel_pages_start;

  // See above.
  . = ALIGN(16 * 1024);

  // See above. Refer to bootstrap/aarch64/boot.ld.in regarding identity pages.
  .data.id_pages :
  {
    __kernel_id_pages_start = .;
    . += (16 * 1024);
    . += __page_size;
    __kernel_id_pages_end = .;
  }

  __kernel_id_pages_size = __kernel_id_pages_end - __kernel_id_pages_start;
}
